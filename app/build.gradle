apply plugin: 'com.android.application'

ext.keyPswd = hasProperty("keyPswd")? getProperty("keyPswd"):"Gradledoc"

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "com.githang.gradledoc"
        minSdkVersion 9
        targetSdkVersion 22
        versionCode "git rev-list HEAD --count".execute().text.trim().toInteger()
        versionName "0.5"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        myConfig {
            storeFile file("../gradle.jks")
            storePassword "Gradle"
            keyAlias "Gradle"
            keyPassword keyPswd
        }
    }

    buildTypes {
        debug {
            signingConfig  signingConfigs.myConfig
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig  signingConfigs.myConfig
        }
    }

    android.applicationVariants.all { variant ->
        if(variant.buildType.name == 'release') {
            variant.outputs.each { output ->
                def file = output.outputFile
                output.outputFile = new File(file.parent, "GradleUserGuide-"
                        + defaultConfig.versionName + "-" + variant.name + ".apk")
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:support-v4:24.2.0'
    compile 'com.android.support:design:24.2.0'
    compile 'com.android.support:cardview-v7:24.2.0'

    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'org.jsoup:jsoup:1.8.1'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'
    // 友盟
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.githang:umeng-update:2.6.0.1'
    // json
    compile 'com.alibaba:fastjson:1.2.4'
    // android代码片段库
    compile 'com.githang:androidsnippet:0.6'
    // 状态栏颜色
    compile 'com.githang:status-bar-compat:0.4.1'
    // rxjava, rxandroid
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'
}

def token = hasProperty("FIR_TOKEN") ? getProperty("FIR_TOKEN") : ""

apply plugin: 'fir'

fir {
    apiToken token
    bundleId android.defaultConfig.applicationId
    appName "Gradle 用户指南"
    icon "./app/src/main/res/drawable-xxhdpi/ic_launcher.png"
    changeLog "内部重构，代码及界面优化"
}
